plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.4'
    id "io.github.goooler.shadow" version '8.1.7'
}

group = 'no.vestlandetmc'
version = '1.7.2'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.glaremasters.me/repository/bloodshot/' }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    compileOnly 'org.spigotmc:spigot-api:1.21-R0.1-SNAPSHOT'
    compileOnly 'com.github.GriefPrevention:GriefPrevention:16.18.4'
    compileOnly 'com.griefdefender:api:2.1.0-SNAPSHOT'
    implementation 'org.bstats:bstats-bukkit:3.0.2'
}

shadowJar {
    relocate 'org.bstats', 'no.vestlandetmc.BanFromClaim.bstats'
    archiveFileName.set('Banfromclaim.jar')
}

jar.dependsOn shadowJar

jar {
    enabled = false
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
